xquery version "3.1";
(:
 : For LICENSE-Details please refer to the LICENSE file in the root directory of this repository.
 :)

(:~
 : This module provides library utility functions
 :
 :)

module namespace eutil = "http://www.edirom.de/xquery/eutil";

(: IMPORTS ================================================================= :)

import module namespace functx = "http://www.functx.com";

(: NAMESPACE DECLARATIONS ================================================== :)

declare namespace edirom="http://www.edirom.de/ns/1.3";
declare namespace mei="http://www.music-encoding.org/ns/mei";
declare namespace request="http://exist-db.org/xquery/request";
declare namespace session="http://exist-db.org/xquery/session";
declare namespace system="http://exist-db.org/xquery/system";
declare namespace tei="http://www.tei-c.org/ns/1.0";
declare namespace util="http://exist-db.org/xquery/util";
declare namespace pref="http://www.edirom.de/ns/prefs/1.0";
declare namespace xmldb="http://exist-db.org/xquery/xmldb";

(: VARIABLE DECLARATIONS =================================================== :)

(:
    Determine the application root collection from the current module load path.
:)
declare variable $eutil:app-root as xs:string :=
    let $rawPath := replace(system:get-module-load-path(), '/null/', '//')
    let $modulePath :=
        (: strip the xmldb: part :)
        if (starts-with($rawPath, "xmldb:exist://")) then
            if (starts-with($rawPath, "xmldb:exist://embedded-eXist-server")) then
                substring($rawPath, 36)
            else if (contains($rawPath, "/xmlrpc/")) then
                substring-after($rawPath, "/xmlrpc")
            else
                substring($rawPath, 15)
        else
            $rawPath
    return
        substring-before($modulePath, "/data/xqm")
;
declare variable $eutil:INVALID_LANGUAGE_CODE := QName("http://www.edirom.de/xquery/eutil", "InvalidLanguageCodeError");
declare variable $eutil:INVALID_DOCUMENT_URI := QName("http://www.edirom.de/xquery/eutil", "InvalidDocumentUriError");
declare variable $eutil:default-prefs-location as xs:string := $eutil:app-root || '/data/prefs/edirom-prefs.xml';
declare variable $eutil:xsltBase as xs:string := $eutil:app-root || '/data/xslt';
declare variable $eutil:supported-languages :=
    (: Extract supported languages from the provided langFiles :)
    collection($eutil:app-root || '/data/locale')//langFile/data(lang);
declare variable $eutil:langDoc :=
    function($lang as xs:string?) as document-node()? {
        collection($eutil:app-root || '/data/locale')//langFile/lang[.=eutil:getSetLanguage($lang)]/root()
        (:eutil:getDoc('../locale/edirom-lang-' || eutil:getSetLanguage($lang) || '.xml'):)
    };

(: FUNCTION DECLARATIONS =================================================== :)

(:~
 : Returns the namespace (standardized prefix)
 :
 : @param $node The node to be processed
 : @return The namespace (prefix)
 :)
declare function eutil:getNamespace($node as node()) as xs:string {

  switch (namespace-uri($node))
    case 'http://www.music-encoding.org/ns/mei'
        return 'mei'
    case 'http://www.tei-c.org/ns/1.0'
        return 'tei'
    case 'http://www.edirom.de/ns/1.3'
        return 'edirom'
    default
        return 'unknown'

};


(:~
 : Returns a localized string
 :
 : @param $node The node to be processed
 : @return The localized output string
 :)
declare function eutil:getLocalizedName($node as element()) as xs:string {

    eutil:getLocalizedName($node, eutil:getSetLanguage(()))

};


(:~
 : Returns a localized string for a provided language
 :
 : @param $node The node to be processed
 : @param $lang The language for the localized output
 : @return The localized output string
 :)
declare function eutil:getLocalizedName($node as element(), $lang as xs:string) as xs:string {

    (: identify the context for further processing:)
    let $case := ( 'child::mei:title'[$node/mei:title], 'child::mei:name'[$node/mei:name], 
                   'child::mei:label'[$node/mei:label], 'child::edirom:names'[$node/edirom:names],
                   'child::edirom:labels'[$node/edirom:labels],
                   'self::mei:annot'[$node[self::mei:annot]], 'other' )[1]
        

    let $name :=

        (: if current node has child mei:title or mei:name or mei:label :)
        if ($case eq 'child::mei:title' or $case eq 'child::mei:name' or $case eq 'child::mei:label') then (

            let $childNodes := $node/mei:*[local-name() = substring-after($case, 'mei:')]

            (: return most appropriate string, either from children's text or empty string :)
            return
                (
                    string-join($childNodes[@xml:lang = $lang]/text(), ' ')[not(matches(., '^\s*$'))],
                    $childNodes[1]/text(),
                    ''
                )[1]
        )
        
        (: if current node has child edirom:names :)
        else if ($case eq 'child::edirom:names') then (
            if ($lang = $node/edirom:names/edirom:name/@xml:lang) then
                $node/edirom:names/edirom:name[@xml:lang = $lang]/node() || ''
            else
                $node/edirom:names/edirom:name[1]/node() || ''
        )
        
        (: if current node has child edirom:labels :)
        else if ($case eq 'child::edirom:labels') then (
            if ($lang = $node/edirom:labels/edirom:label/@xml:lang) then
                $node/edirom:labels/edirom:label[@xml:lang = $lang]/node() || ''
            else
                $node/edirom:labels/edirom:label[1]/node() || ''
        )
        
        (: if current node is an mei:annot :)
        else if ( $case eq 'self::mei:annot' ) then (
            (: if $node is mei:annot and does not have child mei:title or mei:name (covered by cases above) :)
            let $mdiv.n := eutil:getLanguageString('Movement_n', string(count($node/ancestor::mei:mdiv/preceding-sibling::mei:mdiv) + 1), $lang)
            let $measure := eutil:getLanguageString('Bar_n', $node/ancestor::mei:measure/string(@n), $lang)
            return $mdiv.n || ', ' || $measure
        )

        (: otherwise :)
        else (
            (normalize-space($node))
        )
    
    return
        if($node/edirom:names) then
            ($name)
        else
            (eutil:joinAndNormalize($name))

};


(:~
 : Returns a localized string
 :
 : @param $node The node to be processed
 : @param $lang Optional parameter for lang selection
 : @return The string (normalized space)
 :)
declare function eutil:getLocalizedTitle($node as node(), $lang as xs:string?) as xs:string {

    eutil:getLocalizedTitle($node, $lang, eutil:getLanguageString('no_title', ()))

};

(:~
 : Returns a localized string
 :
 : @param $node The node to be processed
 : @param $lang Optional parameter for lang selection
 : @return The string (normalized space)
 :)
declare function eutil:getLocalizedTitle($node as node(), $lang as xs:string?, $default as xs:string) as xs:string {

    let $namespace := eutil:getNamespace($node)
  
    let $titleMEI :=
        if ($lang != '' and $lang = $node/mei:title[mei:titlePart]/@xml:lang) then
            (eutil:joinAndNormalize($node/mei:title[@xml:lang = $lang]/mei:titlePart, '. '))
        else if ($lang != '' and $lang = $node/mei:title[not(mei:titlePart)]/@xml:lang) then
            (eutil:joinAndNormalize($node/mei:title[@xml:lang = $lang]))
        else
            (eutil:joinAndNormalize(($node//mei:title)[1]))
    
    let $titleTEI :=
        if ($lang != '' and $lang = $node/tei:title/@xml:lang) then
            eutil:joinAndNormalize($node/tei:title[@xml:lang = $lang])
        else
            eutil:joinAndNormalize($node/tei:title[1])
    
    return
        if ($namespace = 'mei' and $titleMEI != '') then
            ($titleMEI)
        else if ($namespace = 'tei' and $titleTEI != '') then
            ($titleTEI)
        else 
            $default

};

(:~
 : Returns a document from internal eXist-db paths only
 :
 : @param $uri The URI of the document to process
 : @return The document
 :)
declare function eutil:getDoc($uri as xs:string?) as document-node()? {
    let $normalizedUri := normalize-space($uri)
    return
        if($normalizedUri eq "")
        then util:log("warn", "No document URI provided")
        else
            if (not(eutil:isInternalDbUri($normalizedUri)))
            then error($eutil:INVALID_DOCUMENT_URI, concat('Blocked non-db URI: ', $normalizedUri))
            else
                if(doc-available($normalizedUri))
                then doc($normalizedUri)
                else util:log("warn", "Unable to load document at " || $normalizedUri)
};

(:~
 : Returns whether a URI points to an internal eXist-db collection under `/db`
 :
 : @param $uri The URI to validate
 : @return `true()` if the URI points to `/db`, otherwise `false()`
 :)
declare %private function eutil:isInternalDbUri($uri as xs:string) as xs:boolean {
    (
        starts-with(normalize-space($uri), '/db/')
        or starts-with(normalize-space($uri), 'xmldb:exist://db/')
        or starts-with(normalize-space($uri), 'xmldb:exist:///db/')
        or starts-with(normalize-space($uri), 'xmldb:exist://embedded-eXist-server/db/')
        or starts-with(normalize-space($uri), 'xmldb:exist:///embedded-eXist-server/db/')
    )
    and
    not(
    starts-with(normalize-space($uri), '/db/system/')
        or starts-with(normalize-space($uri), 'xmldb:exist://db/system/')
        or starts-with(normalize-space($uri), 'xmldb:exist:///db/system/')
        or starts-with(normalize-space($uri), 'xmldb:exist://embedded-eXist-server/db/system/')
        or starts-with(normalize-space($uri), 'xmldb:exist:///embedded-eXist-server/db/system/')
    )
};

(:~
 : Resolves a single reference token to its target element(s) by id, honouring cross-file references.
 :
 : - a fragment-only token (#someId), or a bare id without '#', resolves within $node's own document
 : - a token with a base part (other.xml#someId, http://…#someId) is resolved against $node's
 :   base URI and loaded via eutil:getDoc (empty if that document is unavailable)
 :
 : @param $node      a node from the referencing document (provides the base URI / root)
 : @param $reference a single, space-free reference token, with or without a leading '#'
 : @return the element(s) the reference's id resolves to (empty if unresolvable)
 :)
declare function eutil:get-referenced-element($node as node(), $reference as xs:string) as element()* {
    let $reference := normalize-space($reference)
    return
        if($reference eq "") then ()
        else
            let $hasHash := contains($reference, '#')
            let $base    := if($hasHash) then substring-before($reference, '#') else ''
            let $id      := if($hasHash) then substring-after($reference, '#') else $reference
            let $doc :=
                if($base eq '')
                then $node/root()
                else eutil:getDoc(xs:string(resolve-uri($base, base-uri($node))))
            return $doc/id($id)
};

(:~
 : Returns a part's label (translated if available)
 :
 : @author Dennis Ried
 : @param $partID The xml:id of the Part's node() to process
 : @return The label (translated if available)
 :)
declare function eutil:getPartLabel($measureOrPerfRes as node(), $type as xs:string) as xs:string {

    let $lang := eutil:getSetLanguage(())

    let $part := $measureOrPerfRes/ancestor::mei:part
    let $voiceRef := $part//mei:staffDef/@decls
    let $voiceID := substring-after($voiceRef, '#')

    let $perfResLabel :=
        if($type eq 'measure') then
            ($measureOrPerfRes/ancestor::mei:mei/id($voiceID)/@label)
        else
            ($measureOrPerfRes/@label)

    let $dictKey := 'perfMedium.perfRes.' || functx:substring-before-if-contains($perfResLabel,'.')

    let $label :=
        if(eutil:getLanguageString($dictKey, (), $lang)) then
            (eutil:getLanguageString($dictKey, (), $lang))
        else
            ($perfResLabel)

    let $numbering :=
        for $i in subsequence(tokenize($perfResLabel,'\.'),2)
        where matches($i, '([0-9])|([ivxIVX])')
        return
            upper-case($i)

    return
        eutil:joinAndNormalize(($label, $numbering))

};

(:~
 : Returns a language specific string
 :
 : @param $key The key to search for
 : @param $values The values to replace the placeholders with (from the language string)
 : @return The looked up language string from a language file
 :)
declare function eutil:getLanguageString($key as xs:string, $values as xs:string*) as xs:string? {

    eutil:getLanguageString($key, $values, eutil:getSetLanguage(()))
};

(:~
 : Returns a language specific string from the locale/edirom-lang files
 : NB, the project specific language dictionaries are not being queried here.
 : Please use the four-arity version for this!
 :
 : @param $key The key to search for
 : @param $values The values to replace the placeholders with (from the language string)
 : @param $lang The language code (e.g. "de" or "en")
 : @return The looked up language string from a language file
 :)
declare function eutil:getLanguageString($key as xs:string, $values as xs:string*, $lang as xs:string) as xs:string? {

    let $langString := $eutil:langDoc($lang)//entry[@key = $key]/string(@value)

    return
        if($langString) 
        (: replace placeholders in the language string with values provided to the function as parameter :)
        then functx:replace-multi($langString, for $i in (0 to (count($values) - 1)) return concat('\{',$i,'\}'), $values)
        else util:log('error', concat('Failed to find the key `', $key, '` in the Edirom default language file'))

};

(:~
 : Returns a language specific string from the locale/edirom-lang files or project specific language files.
 : The latter takes precedence.
 :
 : @param $langFileURI The URI of the Edition's lang file
 : @param $key The key to search for
 : @param $values The values to replace the placeholders with (from the language string) 
 : @param $lang The language code (e.g. "de" or "en")
 : @return The looked up language string from a language file
 :)
declare function eutil:getLanguageString($langFileURI as xs:string, $key as xs:string, $values as xs:string*, $lang as xs:string) as xs:string? {

    (: Try to load a custom language file :)
    let $langFileCustom := eutil:getDoc($langFileURI)
    
    let $langString :=
        (: If there is a value for the key in the custom language file :)
        if($langFileCustom//entry/@key = $key) then
            $langFileCustom//entry[@key = $key]/@value => string()
        (: If not, take the value for the key in the default language file :)
        else
            $eutil:langDoc($lang)//entry[@key = $key]/@value => string()
    return
        if($langString) 
        (: replace placeholders in the language string with values provided to the function as parameter :)
        then functx:replace-multi($langString, for $i in (0 to (count($values) - 1)) return concat('\{',$i,'\}'), $values)
        else util:log('error', concat('Failed to find the key `', $key, '` in any language file'))
};

(:~
 : Returns a value from the preferences for a given key
 :
 : @param $key The key to look up
 : @param $preferencesURI The URI of the preferences file of the current edition
 : @return The preference value
 :)
declare function eutil:getPreference($key as xs:string, $preferencesURI as xs:string?) as xs:string? {

    (: Try to load a custom preferences file :)
    let $prefFileCustom := eutil:getDoc($preferencesURI)
    
    return
        (: If there is a value for the key in the custom preferences file :)
        if($prefFileCustom//(pref:entry|entry)/@key = $key) then
            $prefFileCustom//(pref:entry|entry)[@key = $key]/@value => string()
        (: If not, take the value for the key in the default preferences file :)
        else
            let $defaultPrefs := eutil:getDoc($eutil:default-prefs-location)
            return
                if($defaultPrefs//(pref:entry|entry)[@key = $key])
                then $defaultPrefs//(pref:entry|entry)[@key = $key]/@value => string()
                (: If the key is not in the default file, then there should be an error :)
                else util:log-system-out(concat('Failed to find the key `', $key, '` in default preferences file'))

};

(:~
 : Get and set the application language
 :
 : If a `$lang` parameter is provided and it is supported by some langFile, the same value will be returned
 :      and saved to the current session. If the provided value is not supported, an error will be raised.
 : If no `$lang` parameter is provided (i.e. `$lang` equals the empty-sequence), the function will try
 :      to determine the language in the following order:
 : 1. from the HTTP request parameter "lang"
 : 2. from the session attribute "lang"
 : 3. from the preferred browser language as provided in the "Accept-Language" header
 : 4. from the first supported language provided by `$eutil:supported-languages`
 :
 : @param $lang The language code (e.g. "de" or "en")
 : @return The language key
 :)
declare function eutil:getSetLanguage($lang as xs:string?) as xs:string? {

    if ($lang)
    then
        if ($lang = $eutil:supported-languages)
        then (
            $lang,
            session:set-attribute('lang', $lang)
        )
        else (
            error($eutil:INVALID_LANGUAGE_CODE, 'Language code "' || $lang || '" is not supported. Please try with "' || string-join($eutil:supported-languages, '", "') || '".')
        )
    else if (request:get-parameter('lang', '') = $eutil:supported-languages)
    then (
        request:get-parameter('lang', ''),
        session:set-attribute('lang', request:get-parameter('lang', ''))
    )
    else if(session:exists() and session:get-attribute('lang') = $eutil:supported-languages)
    then
        session:get-attribute('lang')
    else if(eutil:request-lang-preferred-iso639() = $eutil:supported-languages)
    then (
        eutil:request-lang-preferred-iso639(),
        session:set-attribute('lang', eutil:request-lang-preferred-iso639())
    )
    else
        $eutil:supported-languages[1]
};

(:~
 : Returns the application base URL as seen from the client
 :
 : NB, this is a relative path on the server, missing the scheme,
 : as well as the server address and port.
 : This function simply concats the current context path with the
 : eXist variables `$exist:prefix` and `$exist:controller`
 : (see https://exist-db.org/exist/apps/doc/urlrewrite)
 :
 : @return a relative path on the server
 :)
declare function eutil:get-app-base-url() as xs:string? {

    if(request:exists()) then
        request:get-context-path() || request:get-attribute("exist:prefix") || request:get-attribute('exist:controller')
    else
        util:log-system-out('request object does not exist; failing to compute base url')
};

(:~
 : Sorts a sequence of numeric-alpha values or nodes (e.g. 1, 1a, 1b, 2) 
 : This is an adaption of functx:sort-as-numeric()
 :
 : @author  Dennis Ried
 : @see     http://www.xqueryfunctions.com/xq/functx_sort-as-numeric.html 
 : @param   $seq the sequence to sort 
 :)
declare function eutil:sort-as-numeric-alpha($seq as item()* )  as item()* {

   for $item in $seq
   let $itemPart1 := (functx:get-matches($item, '\d+'))[1]
   let $itemPart2 := substring-after($item, $itemPart1)
   order by number($itemPart1), $itemPart2
   return $item

} ;


(:~
 : Computes a sort key for numeric-alpha values or nodes (e.g. 1, 1a, 1b, 2)
 :
 : @see     http://www.xqueryfunctions.com/xq/functx_compute-sort-key.html
 : @param   $key the key to compute the sort key for
 : @return  the computed sort key
 :)
declare function eutil:compute-measure-sort-key( $key as xs:string ) as xs:string {
    
    let $itemPart1 := (functx:get-matches($key, '\d+'))[1]
    let $keylength := string-length($itemPart1)
    let $prefix := functx:repeat-string('0', 30 - $keylength)

    return concat($prefix, $key)
    
};


(:~
 : Checks if an item is considered empty according to various criteria.
 : An item is considered empty if it is:
 : - An empty sequence ()
 : - An empty string ""
 : - A sequence containing only one empty string ("")
 : - An empty array []
 : - An empty map map{}
 :
 : @param $item the item to check for emptiness
 : @return true if the item is empty, false otherwise
 :)
declare function eutil:is-empty($item) as xs:boolean {

    empty($item) or
    ($item instance of xs:string and $item = "") or
    (count($item) = 1 and $item instance of xs:string and $item = "") or
    ($item instance of array(*) and array:size($item) = 0) or
    ($item instance of map(*) and map:size($item) = 0)

};

(:~
 : Extracts an ISO 639 language code from a given ISO 3166-1 language code
 :
 : @author Benjamin W. Bohl
 : @param  $iso3166-1 xs:string the given ISO 3166-1 language code, e.g., en-US
 : @return xs:string ISO 639 language code, e.g., en
 :)
declare function eutil:iso3166-1-to-iso639($iso3166-1 as xs:string) as xs:string {

    tokenize($iso3166-1, "-")[1]

};

(:~
 : Returns the ISO 639 language code with the highest 'quality' (none considered as 1) from
 : the HTTP-request Accept-Language header
 :
 : @author Benjamin W. Bohl
 : @return xs:string ISO 639 language code
 :)
declare function eutil:request-lang-preferred-iso639() as xs:string? {

    let $request.accept-language := request:get-header("Accept-Language")
    return
        if($request.accept-language) then
            let $tokens := tokenize($request.accept-language, ";")
            
            let $tokens.qless.ordered := (
                for $token in $tokens
                let $q := substring-after(string-join((analyze-string($token, "(q=\d(\.\d)?)")//fn:match)[1], ""), "q=")
                let $q.decimal := if($q = "") then xs:decimal(1) else xs:decimal($q)
                let $token.qless := replace($token,",?q=\d(\.\d)?,?", "")
                order by $q.decimal descending
                return
                    $token.qless
            )
            
            let $tokens.qmax := $tokens.qless.ordered[1]
            let $tokens.qmax.first := tokenize($tokens.qmax, ",")[1]
            return 
                eutil:iso3166-1-to-iso639($tokens.qmax.first)
        
        else
            ()

};

(:~
 : Returns one joined and normalized string
 :
 : @param $strings The string(s) to be processed
 : @return The string (joined with whitespace and normalized space)
 :)
declare function eutil:joinAndNormalize($strings as xs:string*) as xs:string {
    $strings => string-join(' ') => normalize-space()
};

(:~
 : Returns one joined and normalized string
 :
 : @param $strings The string(s) to be processed
 : @param $separator One ore more characters as separators for joining the string
 : @return The string (joined and normalized space)
 :)
declare function eutil:joinAndNormalize($strings as xs:string*, $separator as xs:string) as xs:string {
    $strings => string-join($separator) => normalize-space()
};

(:~
 : Returns a copy of a document with xml:id attributes added to elements missing one
 :
 : @param $doc The document to be processed
 : @return The copied document with xml:id attributes
 :)
declare function eutil:add-xml-ids(
    $doc as document-node()
) as document-node() {
    document {
        for $node in $doc/node()
        return eutil:add-xml-id-to-node($node)
    }
};

(:~
 : Returns a copy of a node with xml:id attributes added recursively to elements missing one
 :
 : @param $node The node to be processed
 : @return The copied node sequence with xml:id attributes
 :)
declare function eutil:add-xml-id-to-node(
    $node as node()
) as node()* {
    typeswitch($node)

        case element() return
        element { node-name($node) } {
            if ($node/@xml:id)
            then $node/@*
            else (
                attribute xml:id { generate-id($node) },
                $node/@*
            ),
            for $child in $node/node()
                return eutil:add-xml-id-to-node($child)
        }

        default return
            $node
};